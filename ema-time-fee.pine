//@version=4
// author: codachang0726
strategy(title = "MA cross plus time period", shorttitle = "MA+time", overlay = true, precision = 8, max_bars_back = 200, pyramiding = 0, initial_capital = 1000, 
     currency = currency.NONE, default_qty_type = strategy.percent_of_equity, default_qty_value = 100, commission_type = "percent", commission_value = 0.1)

// === INPUT MA LENGTHS ===
fastMA    = input(defval = 7,   title = "FastMA",          type = input.integer, minval = 1, step = 1)
slowMA    = input(defval = 14,   title = "SlowMA",          type = input.integer, minval = 1, step = 1)

// === INPUT BACKTEST RANGE ===
fromMonth = input(defval = 1,    title = "From Month",      type = input.integer, minval = 1, maxval = 12)
fromDay   = input(defval = 1,    title = "From Day",        type = input.integer, minval = 1, maxval = 31)
fromYear  = input(defval = 2018, title = "From Year",       type = input.integer, minval = 1970)
thruMonth = input(defval = 1,    title = "Thru Month",      type = input.integer, minval = 1, maxval = 12)
thruDay   = input(defval = 1,    title = "Thru Day",        type = input.integer, minval = 1, maxval = 31)
thruYear  = input(defval = 2019, title = "Thru Year",       type = input.integer, minval = 1970)

// === INPUT SHOW PLOT ===
showDate  = input(defval = true, title = "Show Date Range", type = input.bool)

// === FUNCTION EXAMPLE ===
start     = timestamp(fromYear, fromMonth, fromDay, 00, 00)        // backtest start window
finish    = timestamp(thruYear, thruMonth, thruDay, 23, 59)        // backtest finish window
window()  => time >= start and time <= finish ? true : false       // create function "within window of time"

// === LOGIC ===
crossOv   = crossover (sma(close, fastMA), sma(close, slowMA))     // true when fastMA crosses over slowMA
crossUn   = crossunder(sma(close, fastMA), sma(close, slowMA))     // true when fastMA crosses under slowMA

// DI+ADX
adxlen      = input(14, title="ADX Smoothing")
dilen       = input(14, title="DI Period")
keyLevel    = input(20, title="Keylevel for ADX")
[diplus, diminus, adx] = dmi(dilen, adxlen)
di = (diplus - diminus)

// === EXECUTION ===
strategy.entry("L", strategy.long, when = window() and di > 0 and crossOv and adx > keyLevel)    // enter long when "within window of time" AND crossover
strategy.close("L", when = window() and crossUn and di < 0 and adx > keyLevel )                   // exit long when "within window of time" AND crossunder         

// === PLOTTING ===
bgcolor(color = showDate and window() ? color.gray : na, transp = 90)                                     // plot "within window of time"
plot(sma(close, fastMA), title = 'FastMA', color = color.yellow, linewidth = 2, style = plot.style_line)  // plot FastMA
plot(sma(close, slowMA), title = 'SlowMA', color = color.aqua,   linewidth = 2, style = plot.style_line)  // plot SlowMA
